{-# OPTIONS_GHC -fno-warn-incomplete-patterns #-}
module QFeldspar.Eta
       (eta) where

import QFeldspar.MyPrelude

import QFeldspar.Expression.GADTHigherOrder
import QFeldspar.Expression.Utils.GADTHigherOrder(absTmp)
import QFeldspar.Expression.Utils.Common
import QFeldspar.Singleton
import qualified QFeldspar.Type.GADT as TG

eta :: forall n t. TG.Type t =>
       Exp n t -> Exp n t
eta e = let t = sin :: TG.Typ t in
  case e of
    Abs eb -> case TG.getPrfHasSinArr t of
        (PrfHasSin , PrfHasSin) -> Abs (etaF eb)
    _      -> case t of
      TG.Arr _ _               -> case TG.getPrfHasSinArr t of
        (PrfHasSin , PrfHasSin) -> Abs (\ x -> eta (App e x))
      _                         -> etasub e

etasub :: forall n t. TG.Type t =>
          Exp n t -> Exp n t
etasub ee = let t = sin :: TG.Typ t in case ee of
  App ef ea -> App (etasub ef) (eta ea)
  _         -> $(genOverloadedW 'ee ''Exp  ['App] (trvWrp 't)
   (\ tt -> if
    | matchQ tt [t| Exp t t -> Exp t t |] -> [| etaF |]
    | matchQ tt [t| Exp t t |]            -> [| eta |]
    | otherwise                           -> [| id |]))

etaF :: forall n ta tb. (TG.Type ta , TG.Type tb) =>
           (Exp n ta -> Exp n tb) -> (Exp n ta -> Exp n tb)
etaF f = let v  = genNewNam "__etaF__"
             {-# NOINLINE v #-}
         in deepseq v $ (\ x -> absTmp x v (eta (f (Tmp v))))
